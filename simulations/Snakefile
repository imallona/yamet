#!/usr/bin/env snakemake -s
##
## Runs CpG coverage simulations
##
## Started 12th Dec 2024

import os.path as op
import pandas as pd

# dictionary with keys being the dataset names => place this in the config
EXTERNAL_ORIGIN = {
    "lowReal": "ftp://ftp.ebi.ac.uk/pub/databases/scnmt_gastrulation/scnmt_gastrulation.tar.gz",
}


rule all:
    input:
        "output/02_coverage_plots.html",
        "output/03_yamet_plots.html",
        "output/04_feat_diff.html",
        "output/05_within_cell.html",
        "output/06_between_cell.html",


# every row in the parameter file is a parameterization of one simulation
rule check_required_data:
    input:
        parameters_file="input/parameters.tsv",
    output:
        required_data="data/required_data.tsv",
    run:
        required_datasets = []
        df = pd.read_csv(input.parameters_file, sep="\t")
        for index, row in df.iterrows():
            print(row["covParams"])
            if row["covParams"] in EXTERNAL_ORIGIN.keys():
                required_datasets.append(row["covParams"])
            elif row["transMat"] in EXTERNAL_ORIGIN.keys():
                required_datasets.append(row["transMat"])
            else:
                continue
        required_datasets = list(set(required_datasets))
        with open(output.required_data, "a") as file:
            for dataset in required_datasets:
                file.write(dataset + "\n")


rule download_data:
    input:
        required_data="data/required_data.tsv",
    output:
        downloaded_data="data/downloaded_data.tsv",
    threads: 2
    params:
        external_origin=lambda wildcards: " ".join(
            f"{k}={v}" for k, v in EXTERNAL_ORIGIN.items()
        ),
    shell:
        """
          # Convert params.external_origin into a key-value file
          echo "{params.external_origin}" | tr ' ' '\\n' | awk -F= '{{{{print $1"\\t"$2}}}}' > external_origin.tsv

          while read -r key; do
              value=$(awk -v k="$key" '$1 == k {{{{print $2}}}}' external_origin.tsv)
              echo "$key -> $value"
              curl --output "data/$key.tar.gz" "$value"
              printf "%s\\t%s\\n" "$key" "$value" >> "{output.downloaded_data}"
              mkdir -p "data/$key" && tar -xzvf "data/$key.tar.gz" -C "data/$key/"
          done < "{input.required_data}"
        """


rule simulate_data:
    input:
        parameters_path="input/parameters.tsv",
        downloaded_data="data/downloaded_data.tsv",
    output:
        directory("output/sim_data"),
        html_output_path="output/01_sim_data.html",
    conda:
        "envs/r_sim.yaml"
    shell:
        """
          Rscript -e 'rmarkdown::render("src/01_sim_data.Rmd", "html_document", 
                                        output_file="../{output.html_output_path}",
                                        params=list(parameters_path="{input.parameters_path}"))'
        """


rule visualize_coverage:
    input:
        sim_data_file="output/01_sim_data.html",
        sim_data_dir="output/sim_data",
    output:
        html_output_path="output/02_coverage_plots.html",
    conda:
        "envs/r_plot.yaml"
    shell:
        """
          Rscript -e 'rmarkdown::render("src/02_coverage_plots.Rmd", "html_document", 
                                        output_file="../{output.html_output_path}",
                                        params=list(sim_data_dir="{input.sim_data_dir}"))'
        """


rule install_yamet:
    conda:
        "envs/yamet.yaml"
    output:
        op.join("build", "yamet"),
    shell:
        """
          bash ../method/build.sh
        """


rule run_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        yamet=op.join("build", "yamet"),
        sim_data=op.join("output", "sim_data"),
    output:
        out=directory(op.join("output", "yamet", "out")),
    threads: 6
    params:
        ncpgs="50 100 1000 10000",
    shell:
        """
          mkdir -p {output.out}
          for ncpg in {params.ncpgs}; do
          {input.yamet} --cell {input.sim_data}/sim_cell_*_*_${{ncpg}}_*.tsv \
                        --reference {input.sim_data}/cpgPositions_${{ncpg}}.tsv \
                        --intervals src/yamet_search.tsv \
                        --out {output.out}/${{ncpg}}.tsv \
                        --cores {threads} \
                        --print-sampens F
          done
        """


rule visualize_yamet_results:
    conda:
        "envs/r_plot.yaml"
    input:
        yamet_dir="output/yamet/out",
    output:
        html="output/03_yamet_plots.html",
    shell:
        """
          Rscript -e 'rmarkdown::render("src/03_yamet_plots.Rmd", "html_document", 
                                        output_file="../{output.html}",
                                        params=list(yamet_dir="{input.yamet_dir}"))'
        """


rule feat_diff_template:
    conda:
        "envs/r_sim.yaml"
    output:
        "output/feat_diff/intervals.{N}.{f}.tsv",
    script:
        "src/feat_diff/createTemplate.R"


rule feat_diff_samples:
    conda:
        "envs/r_sim.yaml"
    input:
        "output/feat_diff/intervals.{N}.{f}.tsv",
    output:
        touch("output/feat_diff/{N}.{f}.flag"),
    params:
        samples=50,
        data="output/feat_diff",
    script:
        "src/feat_diff/getSamples.R"


rule feat_diff_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        "output/feat_diff/{N}.{f}.flag",
        yamet=op.join("build", "yamet"),
    output:
        det_out="output/feat_diff/yamet.{N}.{f}.det.out",
    params:
        data="output/feat_diff",
    threads: 3
    shell:
        """
          {input.yamet} --cell {params.data}/sim.*.tsv \
                        --reference {params.data}/ref.{wildcards.N}.{wildcards.f}.tsv \
                        --intervals {params.data}/intervals.{wildcards.N}.{wildcards.f}.tsv \
                        --det-out {output.det_out} \
                        --meth-out {params.data}/yamet.{wildcards.N}.{wildcards.f}.meth.out \
                        --cores {threads} \
                        --print-sampens F
        """


rule feat_diff_plots:
    conda:
        "envs/r_plot.yaml"
    input:
        det_out="output/feat_diff/yamet.500.200.det.out",
    params:
        N=lambda wildcards, input: input.det_out.split(".")[1],
        f=lambda wildcards, input: input.det_out.split(".")[2],
    output:
        html="output/04_feat_diff.html",
    script:
        "src/feat_diff/04_feat_diff.Rmd"


rule within_cell_template:
    conda:
        "envs/r_sim.yaml"
    output:
        "output/within_cell/intervals.{S}.{N}.{f}.tsv",
    script:
        "src/within_cell/createTemplate.R"


rule within_cell_samples:
    conda:
        "envs/r_sim.yaml"
    input:
        "output/within_cell/intervals.{S}.{N}.{f}.tsv",
    output:
        "output/within_cell/sim.{s}.{S}.{N}.{f}.tsv",
    params:
        data="output/within_cell",
    script:
        "src/within_cell/getSamples.R"


rule within_cell_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        cells=lambda wildcards: expand(
            "output/within_cell/sim.{s}.{{S}}.{{N}}.{{f}}.tsv",
            s=range(1, int(wildcards.S) + 1),
        ),
        yamet=op.join("build", "yamet"),
    output:
        det_out="output/within_cell/yamet.{S}.{N}.{f}.det.out",
    params:
        data="output/within_cell",
    threads: 4
    benchmark:
        repeat("output/within_cell/yamet.{S}.{N}.{f}.benchmark", 5)
    shell:
        """
          {input.yamet} --cell {input.cells} \
                        --reference {params.data}/ref.{wildcards.N}.{wildcards.f}.tsv \
                        --intervals {params.data}/intervals.{wildcards.S}.{wildcards.N}.{wildcards.f}.tsv \
                        --skip-header-intervals \
                        --det-out {output.det_out} \
                        --meth-out {params.data}/yamet.{wildcards.S}.{wildcards.N}.{wildcards.f}.meth.out \
                        --cores {threads} \
                        --print-sampens F
        """


rule within_cell_scmet:
    conda:
        "envs/r_plot.yaml"
    input:
        "output/within_cell/yamet.{S}.{N}.{f}.det.out",
    output:
        "output/within_cell/scmet.{S}.{N}.{f}.rds",
    threads: 4
    benchmark:
        repeat("output/within_cell/scmet.{S}.{N}.{f}.benchmark", 5)
    script:
        "src/within_cell/scmet.R"


S_GRID = {"S": [20, 40, 60, 80, 100], "N": 300, "f": 641}
N_GRID = {"S": 30, "N": [100, 200, 300, 400, 500], "f": 641}
DISP = {"S": 30, "N": 300, "f": 1281}


# specify the input yamet file with the specifications needed in the form
# yamet.{number of samples}.{number of features}.{length of each feature}.det.out
# the length of each feature here must be of the form 8n+1
rule within_cell_plots:
    conda:
        "envs/r_plot.yaml"
    input:
        s_scmets=expand(
            "output/within_cell/scmet.{S}.{N}.{f}.rds",
            S=S_GRID["S"],
            N=S_GRID["N"],
            f=S_GRID["f"],
        ),
        n_scmets=expand(
            "output/within_cell/scmet.{S}.{N}.{f}.rds",
            S=N_GRID["S"],
            N=N_GRID["N"],
            f=N_GRID["f"],
        ),
        disp_scmet=f"output/within_cell/scmet.{DISP["S"]}.{DISP["N"]}.{DISP["f"]}.rds",
    params:
        S_GRID=S_GRID,
        N_GRID=N_GRID,
        DISP=DISP,
    threads: 4
    output:
        html="output/05_within_cell.html",
    script:
        "src/within_cell/05_within_cell.Rmd"


rule within_cell_old_template:
    conda:
        "envs/r_sim.yaml"
    output:
        "output/within_cell_old/intervals.{N}.{f}.tsv",
    script:
        "src/within_cell_old/createTemplate.R"


rule within_cell_old_samples:
    conda:
        "envs/r_sim.yaml"
    input:
        "output/within_cell_old/intervals.{N}.{f}.tsv",
    output:
        touch("output/within_cell_old/{samples}.{N}.{f}.flag"),
    params:
        data="output/within_cell_old",
    script:
        "src/within_cell_old/getSamples.R"


rule within_cell_old_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        "output/within_cell_old/{samples}.{N}.{f}.flag",
        yamet=op.join("build", "yamet"),
    output:
        det_out="output/within_cell_old/yamet.{samples}.{N}.{f}.det.out",
    params:
        data="output/within_cell_old",
    threads: 4
    shell:
        """
          {input.yamet} --cell {params.data}/sim.*.{wildcards.samples}.{wildcards.N}.{wildcards.f}.tsv \
                        --reference {params.data}/ref.{wildcards.N}.{wildcards.f}.tsv \
                        --intervals {params.data}/intervals.{wildcards.N}.{wildcards.f}.tsv \
                        --det-out {output.det_out} \
                        --meth-out {params.data}/yamet.{wildcards.samples}.{wildcards.N}.{wildcards.f}.meth.out \
                        --cores {threads} \
                        --print-sampens F
        """


# specify the input yamet file with the specifications needed in the form
# yamet.{number of samples}.{number of features}.{length of each feature}.det.out
rule within_cell_old_plots:
    conda:
        "envs/r_plot.yaml"
    input:
        det_out="output/within_cell_old/yamet.50.200.100.det.out",
    params:
        samples=lambda wildcards, input: input.det_out.split(".")[1],
        N=lambda wildcards, input: input.det_out.split(".")[2],
        f=lambda wildcards, input: input.det_out.split(".")[3],
    threads: 4
    output:
        html="output/within_cell_old.html",
    script:
        "src/within_cell_old/within_cell_old.Rmd"


rule between_cell_old_template:
    conda:
        "envs/r_sim.yaml"
    output:
        "output/between_cell_old/intervals.{N}.{f}.tsv",
    script:
        "src/between_cell_old/createTemplate.R"


rule between_cell_old_samples:
    conda:
        "envs/r_sim.yaml"
    input:
        "output/between_cell_old/intervals.{N}.{f}.tsv",
    output:
        touch("output/between_cell_old/{N}.{f}.flag"),
    params:
        samples=50,
        data="output/between_cell_old",
    script:
        "src/between_cell_old/getSamples.R"


rule between_cell_old_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        "output/between_cell_old/{N}.{f}.flag",
        yamet=op.join("build", "yamet"),
    output:
        det_out="output/between_cell_old/yamet.{N}.{f}.det.out",
    params:
        data="output/between_cell_old",
    threads: 3
    shell:
        """
          {input.yamet} --cell {params.data}/sim.*.tsv \
                        --reference {params.data}/ref.{wildcards.N}.{wildcards.f}.tsv \
                        --intervals {params.data}/intervals.{wildcards.N}.{wildcards.f}.tsv \
                        --det-out {output.det_out} \
                        --meth-out {params.data}/yamet.{wildcards.N}.{wildcards.f}.meth.out \
                        --cores {threads} \
                        --print-sampens F
        """


rule between_cell_old_plots:
    conda:
        "envs/r_plot.yaml"
    input:
        det_out="output/between_cell_old/yamet.200.100.det.out",
    params:
        N=lambda wildcards, input: input.det_out.split(".")[1],
        f=lambda wildcards, input: input.det_out.split(".")[2],
    output:
        html="output/between_cell_old.html",
    script:
        "src/between_cell_old/between_cell_old.Rmd"


rule between_cell_template:
    conda:
        "envs/r_sim.yaml"
    output:
        "output/between_cell/intervals.{N}.{f}.tsv",
    script:
        "src/between_cell/createTemplate.R"


rule between_cell_samples:
    conda:
        "envs/r_sim.yaml"
    input:
        "output/between_cell/intervals.{N}.{f}.tsv",
    output:
        touch("output/between_cell/{samples}.{N}.{f}.flag"),
    params:
        data="output/between_cell",
    script:
        "src/between_cell/getSamples.R"


rule between_cell_yamet:
    conda:
        "envs/yamet.yaml"
    input:
        "output/between_cell/{samples}.{N}.{f}.flag",
        yamet=op.join("build", "yamet"),
    output:
        det_out="output/between_cell/yamet.{samples}.{N}.{f}.det.out",
    params:
        data="output/between_cell",
    threads: 3
    shell:
        """
          {input.yamet} --cell {params.data}/sim.*.{wildcards.samples}.{wildcards.N}.{wildcards.f}.tsv \
                        --reference {params.data}/ref.{wildcards.N}.{wildcards.f}.tsv \
                        --intervals {params.data}/intervals.{wildcards.N}.{wildcards.f}.tsv \
                        --det-out {output.det_out} \
                        --meth-out {params.data}/yamet.{wildcards.samples}.{wildcards.N}.{wildcards.f}.meth.out \
                        --cores {threads} \
                        --print-sampens F
        """


# specify the input yamet file with the specifications needed in the form
# yamet.{number of samples}.{number of features}.{length of each feature}.det.out
rule between_cell_plots:
    conda:
        "envs/r_plot.yaml"
    input:
        det_out="output/between_cell/yamet.300.200.100.det.out",
    params:
        samples=lambda wildcards, input: input.det_out.split(".")[1],
        N=lambda wildcards, input: input.det_out.split(".")[2],
        f=lambda wildcards, input: input.det_out.split(".")[3],
    output:
        html="output/06_between_cell.html",
    script:
        "src/between_cell/06_between_cell.Rmd"
